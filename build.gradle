plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.6.0' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group 'xyz.oribuin.skyblock'
    version '1.0'

    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url "https://repo.rosewooddev.io/repository/public/" }
        maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
        maven { url "https://repo.codemc.org/repository/maven-public/" }
        maven { url "https://maven.enginehub.org/repo/" }
        maven { url 'https://jitpack.io' }

        flatDir { dirs 'libs' }
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS:Wrapper')
    implementation project(':NMS:v1_16_R1')
    implementation project(':NMS:v1_16_R2')
    implementation project(':NMS:v1_16_R3')
    implementation project(path: ':NMS:v1_17_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_18_R1', configuration: 'remapped')
}

shadowJar {
    archiveClassifier.set(null)
    relocate "com.zaxxer.hikari", "${project.group}.libs.hikari"
    relocate "io.github.bananapuncher714.nbteditor", "${project.group}.libs.nbteditor"
    relocate "kotlin", "${project.group}.libs.kotlin"
    relocate "org.intellij", "${project.group}.libs.intellij"
    relocate "org.slf4j", "${project.group}.libs.slf4j"
    relocate "xyz.oribuin.orilibrary", "${project.group}.libs.cmdlib"
    relocate "xyz.oribuin.gui", "${project.group}.libs.gui"
    relocate "net.wesjd.anvilgui", "${project.group}.libs.anvilgui"
}

tasks.getByName("build").dependsOn("shadowJar")