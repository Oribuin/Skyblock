plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id "io.papermc.paperweight.userdev" version '1.5.13'
}

group = 'xyz.oribuin.skyblock'
version = '1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://libraries.minecraft.net" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.rosewooddev.io/repository/public/" }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://repo.md-5.net/content/repositories/public/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://jitpack.io/" }
}

dependencies {
    paperweight.paperDevBundle("${spigotVersion}-R0.1-SNAPSHOT")

    // Spigot Dependencies
    compileOnly "io.papermc.paper:paper-api:${spigotVersion}-R0.1-SNAPSHOT"
    compileOnly "com.mojang:authlib:1.5.21"
    compileOnly "org.jetbrains:annotations:24.0.1"


    // Plugin APIs
    compileOnly "me.clip:placeholderapi:2.11.3"
    compileOnly "com.sk89q.worldedit:worldedit-bukkit:7.3.0-SNAPSHOT"
    compileOnly "com.arcaniax:HeadDatabase-API:1.3.1"
    compileOnly("com.github.MilkBowl:VaultAPI:1.7") {
        exclude group: "org.bukkit"
    }

    // Frameworks
    api "dev.rosewood:rosegarden:${gardenVersion}" // https://github.com/Rosewood-Development/RoseGarden
    api "net.wesjd:anvilgui:1.9.3-SNAPSHOT"// https://github.com/WesJD/AnvilGUI
    api("dev.triumphteam:triumph-gui:3.1.7") {  // https://mf.mattstudios.me/triumph-gui/introduction
        exclude group: "com.google.code.gson", module: "gson" // Remove GSON, Already included in spigot api
    }
}

shadowJar {
    archiveClassifier.set(null)

    relocate("dev.rosewood.rosegarden", "${project.group}.libs.rosegarden")
    relocate("net.wesjd.anvilgui", "${project.group}.libs.anvilgui")
    relocate("dev.triumphteam.gui", "${project.group}.libs.triumphgui")
}

tasks {
    build {
        dependsOn("reobfJar")
        dependsOn("shadowJar")
    }
}
